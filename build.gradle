import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'java'
}

group 'com.suave'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

subprojects {
    // 使用插件
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // 项目的默认名称和版本
    group = "com.suave"
    version = '0.0.1-SNAPSHOT'

    // 指定JDK版本
    sourceCompatibility = '8'
    jar.enabled = true

    // 指定仓库地址
    repositories {
        mavenCentral()
    }

    dependencyManagement {
        // 导入SpringBoot和SpringCloud依赖Bom
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.1"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:2021.0.1.0"
        }

        // 自定义配置第三方依赖
        dependencies {
            dependency 'mysql:mysql-connector-java:8.0.30'
            dependency 'com.alibaba:druid-spring-boot-starter:1.2.13-SNSAPSHOT'
            dependency 'com.baomidou:mybatis-plus-boot-starter:3.5.2'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    }
}